#!/usr/bin/env perl

#
# Boost lib for modern toolchain.
#

use strict;
use File::Basename;
use File::Find;
use File::Path qw(make_path);
use Cwd qw(abs_path getcwd);
my $cwd = getcwd();

use Getopt::Long::Descriptive;

my($opt, $usage) = describe_options("%c %o [target]",
				    ["valgrind", "Install with valgrind support"],
				    ["subdir=s", "Install into this subdirectory of the target", { default => "build-tools" }],
				    ["help|h", "Show this usage message"]);
print($usage->text), exit if $opt->help;
die($usage->text) if @ARGV > 1;

my $top_dest = $ENV{TARGET} || "/kb/runtime";
if (@ARGV)
{
    $top_dest = shift;
}

my $vers = "1.73.0";
(my $versu = $vers) =~ s/\./_/g;

my $dest = $top_dest;
$dest .= "/boost-$vers";

my @opts;
if ($opt->valgrind)
{
    $dest .= "-valgrind";
    push(@opts, "valgrind=on");
}

my $build_tools = $top_dest . "/" . $opt->subdir;

print "using $dest as installation directory\n";
system ("mkdir", "-p", $dest) unless -e $dest;

$ENV{PATH} = "$dest/bin:$build_tools/bin:$ENV{PATH}";
system("printenv|sort");
my $dir = download_tool("https://boostorg.jfrog.io/artifactory/main/release/1.73.0/source/boost_${versu}.tar.bz2");
#my $dir = download_tool("https://dl.bintray.com/boostorg/release/1.70.0/source/boost_1_70_0.tar.bz2");
#my $dir = download_tool("http://downloads.sourceforge.net/project/boost/boost/1.69.0/boost_1_69_0.tar.bz2");
$dir = abs_path($dir);
do_chdir($dir);
do_chdir("tools/build");
run("./bootstrap.sh");
$ENV{LD_LIBRARY_PATH} = "$build_tools/lib:$build_tools/lib64";
run("./b2", "install", "--prefix=$dest");
do_chdir($dir);


run("b2", "-j", 6, @opts, "toolset=gcc", "stage");
run("b2", @opts, "toolset=gcc", "--prefix=$dest", "install");

unlink("$top_dest/boost-latest");
symlink("boost-$vers", "$top_dest/boost-latest");

sub download_tool
{
    my($url, $dest) = @_;

    my $file = basename($url);
    my($dir,$comp) = $file =~ /^(.*)\.(tar\.gz|tgz|tar\.bz2)$/;

    my %comp_option = ('tar.gz' => 'z',
		       'tgz' => 'z',
		       'tar.bz2' => 'j');
    my $comp_option = $comp_option{$comp};

    if (! -s $file)
    {
	run("curl", "-o", $file, "-L", $url);
    }

    system("rm", "-rf", $dir);
    run("tar",  "xf$comp_option",  $file);

    -d $dir or die "$dir not found after extraction";
    return $dir;
}

sub build_tool
{
    my($dir, $dest) = @_;

    chdir $dir or die "could not chdir $dir: $!";

    run("./configure", "--prefix", $dest);
	
    run("make", "-j", "6");
    run("make", "install");
}
    
sub do_chdir
{
    my($dir) = @_;
    if (!chdir($dir))
    {
	my $here = getcwd();
	die "chdir $dir from $here failed: $!";
    }
}

sub run
{
    my(@cmd) =@_;
    print "@cmd\n";
    my $rc = system(@cmd);
    if ($rc != 0)
    {
	die "Command failed with rc=$rc: @cmd\npath=$ENV{PATH}\n";
    }
}

